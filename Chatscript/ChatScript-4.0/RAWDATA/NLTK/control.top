#### establish the default bot

table: defaultbot (^name)
^createfact(^name defaultbot defaultbot)
DATA:
nltk


#### define the bot

outputmacro: nltk()  # you get safariby default
$control_pre = ~pre_control
$control_main = ~main_control
$control_post = ~post_control
$token =  #DO_ESSENTIALS | #DO_CONTRACTIONS    | #DO_PARSE  | #STRICT_CASING | #NO_HYPHEN_END | #NO_COLON_END | #NO_SEMICOLON_END 

#### define the bot control scripts

topic: ~main_control system repeat ()	# executed for each user sentence of input
u: ( %input<%userfirstline ) 
	^keep()   
	$min = -1
	Legal commands are: \n
	tally basepos - shows all canonical by part of speech \n
	tally base min 2  - shows all canonical with 2 or more hits \n
	tally raw min 2 - shows all original words with 2 or more hits \n
	sentiment  - shows sentiment analysis of successive inputs or of ":document" \n
	reset - clears current mode \n
	- try one of them with ":document REGRESS/moby_dick.txt" \n

# on startup, do introduction
u: (%length=0) fail(topic)

u: (%document _*)	$sentence = '_0 $document = true
		$$linecount += 1
		if (!$sentiment) # when tallying, show progress. Sentiment traces all anyway
		{
			$$tmp = $$linecount % 1000
			if ($$linecount == 10000 ) {Log(OUTPUT_ECHO 1)}
			else if ($$linecount == 20000) {Log(OUTPUT_ECHO 2)}
			else if ($$linecount == 30000) {Log(OUTPUT_ECHO 3)}
			else if ($$tmp == 0) {Log(OUTPUT_ECHO .)}
		}
		refine()
	a: ($tally=raw) respond(~tallyraw) end(topic)
	a: ($tally=base) respond(~tallybase) end(topic)
	a: ($tally=basepos) respond(~tallybasepos) end(topic)
	u: ($sentiment) respond(~sentiment) end(topic)
u: (!%document $sentiment) respond(~sentiment) end(topic)

# cutoff minimum
u: (!%document min _~number) $min = _0

u: (!%document) refine()
	a: (%input<%userfirstline)  NLTK ready. 

	a: (< reset) $tally  = null $sentiment = null
		$token =  #DO_ESSENTIALS | #DO_CONTRACTIONS    | #DO_PARSE  | #STRICT_CASING | #NO_HYPHEN_END | #NO_COLON_END | #NO_SEMICOLON_END 

	a: (< !no tally basepos) $tally = basepos Tallying basepos with min $min

	a: (< !no tally base) $tally = base Tallying base  with min $min

	a: (< !no tally raw) $tally = raw Tallying raw   with min $min $token =  #DO_ESSENTIALS | #DO_CONTRACTIONS  | #STRICT_CASING | #NO_HYPHEN_END | #NO_COLON_END | #NO_SEMICOLON_END 

	a: (< !no sentiment) $sentiment = 1 Doing sentiment

outputmacro: ^show(^word)
	$$shown += 1
	$$tally = ^tally(^word)
	$$len = length($$tally)
	if ($$len == 1) {Log((OUTPUT_ECHO OUTPUT_NOQUOTES ) "0000")}
	else if ($$len == 2) {Log((OUTPUT_ECHO OUTPUT_NOQUOTES ) "000")}
	else if ($$len == 3) {Log((OUTPUT_ECHO OUTPUT_NOQUOTES ) "00")}
	else if ($$len == 4) {Log((OUTPUT_ECHO OUTPUT_NOQUOTES ) "0")}
	Log((OUTPUT_ECHO OUTPUT_NOCOMMANUMBER) $$tally \= ^word  \n)

outputmacro: ^dump(^word)
	if (^tally(^word) >=  $min)
	{
		^show(^word)
	}

topic: ~post_control system repeat()
t:  () if ($tally AND $document) 
	{
		Log(OUTPUT_ECHO \n) 
		$$shown = 0
		walkdictionary('^dump) 
		Log(OUTPUT_ECHO  $$shown unique words. \n ) 
		$document = null
	}
	else if ($sentiment )
	{ 
		$scoregood += 0
		$scorebad += 0
		$$tmp = $scoregood - $scorebad
		Log(OUTPUT_ECHO \n OVERALL RESULT: $$tmp  \( + $scoregood - $scorebad \) \n ) 
	}

topic: ~pre_control system repeat()
t:  ^if (%document)
	{	
		$$linecount = 0
		Log(OUTPUT_ECHO \n Begin $$document )
		if ($sentiment) {Log(OUTPUT_ECHO \n  )  $scoregood = null $scorebad = null}
	}

# its the ONLY possible verb "Carlos smiled."  -- DONT DO that as a phase 1 rule

# verb wont be after a comma unless command infinitive or prior comma exists
-1 IS COMMA
	STAY !PRIORCOMMA
	INCLUDE * VERB_TENSES - VERB_INFINITIVE
DISCARD

#   subject obvious verb followed by to infinitive "I *long to go" but not - "rolls into a tight little *ball to protect itself"
-1 HAS PRONOUN_SUBJECT NOUN_PROPER_SINGULAR NOUN_PROPER_PLURAL NOUN_PLURAL
	INCLUDE * VERB_TENSES
	STAY !HAS NORMAL_NOUN_BITS
	IS TO_INFINITIVE
KEEP

# in a conflict between past and past participle, particple loses if no aux before it - "I have a dog *named Harry" AND not possible adjective_participle
# though it could be adjective_participle
REVERSE 0 INCLUDE * VERB_PAST_PARTICIPLE
	STAY CANONLYBE VERB_PAST VERB_PAST_PARTICIPLE
	SKIP !HAS AUX_VERB_BITS 
	!HAS AUX_VERB_BITS 
DISCARD

# negative after  helper will make next a verb "does not *like" and "would seldom eat" - cant be "do not like harry does" so safe - but not "there is no *question"
-2  IS AUX_VERB_BITS
	PARSEMARK NEGATIVE_SV_INVERTER
	INCLUDE * VERB_TENSES
KEEP

# if can be noun or verb but is preceeded by possessive, will not be verb "he pressed his *nose"
-1 IS POSSESSIVE
	INCLUDE * VERB_TENSES VERB_INFINITIVE
	STAY HAS NOUN_BITS
DISCARD

# not going to have a normal verb in front of an aux (or regular verb)  - aux can is singular noun, aux does is plural. "what *games does he play"
0 INCLUDE * VERB_TENSES
  HAS AUX_VERB_BITS    # except when does is plural of doe "he *shoots does"  a bug
DISCARD

################################## Rules about NOT being some verb form

# present/past participle needs noun before it (would have to be aux or commmand) - and commands are "you" --- BUT Dressed in red  the man (legal) AND so is "is a mirror green" question form
# *driving a car is fun  ILLEGAL: Walks the dog
# no simple past after gerund "is"  # after being reprimanded (legal) BUT swimming was his passion is fine
# a verb cannot have a singular noun immediately after it -- except for mass nouns.  I like water. I like lox.
# I give rock to him -- not legal
# the most rain ever recorded *fell

# sentence start using what as subject/object xxx  requires potential verb to not be VERB_PRESENT but be VERB_PRESENT_3PS "what games the system"  
-1  START
	STAY ISORIGINAL what
	INCLUDE * VERB_PRESENT
DISCARD

# known verb after precludes this as verb - "I *even went home" but adjective particle could be
0	INCLUDE * VERB_TENSES
	IS VERB_TENSES AUX_VERB_BITS
DISCARD

#################### cant be any tense of verb

# following a noun infintive - "I taught Sue how to eat *fish"
-1 IS NOUN_INFINITIVE
	INCLUDE * VERB_TENSES
DISCARD

# "being" w/o aux before it is NOT a verb (noun_singular or noun_gerund) - "*being essential, the flag was taken" "his body, being needed, was kept
-1	!HAS AUX_BE
	ISORIGINAL being
	STAY INCLUDE * VERB_TENSES
DISCARD

# verb cannot follow a pre/determiner or preposition or verb or possesive or or paren  - "the *mangled boats"  will be adjective particple   "what *shape is this"   "I am the happily singing boy"  but beware plural noun "we were singing boys" not handled AND after coord subjunct "who mangled me" is legal
REVERSE 0 INCLUDE * VERB_TENSES
	STAY !ISCANONICAL be		# "his *home was"  "last house to *be flooded"  # the farm in the glen "I love my *parents" "my mother's coat"
	SKIP IS ADVERB_BITS
	IS VERB_TENSES  DETERMINER_BITS PREPOSITION  PAREN   # but a verb can follow a post adjective like "walked"
DISCARD

# verb cannot precede a verb, except for to-less infinitive "Let go of her" - except clause "the dog my dog *likes include"

# verb cannot follow an adjective unless adjective is postpositive OR "the xxx" implied missing noun
# but a verb can follow a post adjective like "walked"

# aux followed by potential verb (not be) cannot be when aux is question "Does sue live here"
-1	IS AUX_VERB_BITS
	STAY START
	INCLUDE * VERB_TENSES 	
	STAY !HAS AUX_VERB_BITS
DISCARD

# verb cannot be followed by possessive - my mother's coat
0 INCLUDE * VERB_TENSES
	IS POSSESSIVE
DISCARD

# cannot have 2 verbs after a comma or paren (2 verbs only on closing a clause) "Rocky, my orange tomcat, loves *having his head scratched"
-2	IS PAREN COMMA
	IS VERB_TENSES				
	INCLUDE * VERB_TENSES
DISCARD

# verb cannot occur before aux
0	INCLUDE * VERB_TENSES
	IS AUX_VERB_BITS
DISCARD

# of cannot follow a verb except for certain ones - idioms like "*tell of"
0	INCLUDE * VERB_TENSES
	STAY !PARSEMARK VERB_ALLOWS_OF_AFTER
	ISORIGINAL of
DISCARD

####################  cant be specific tense kind of verb

# present/past tense regular cannot follow potential conjunction subordinate (needs noun subject), but "I want the one *that dangled here" is a relative clause subject also. - but "Ponto then *found Rose, and pulled her out"

### cant be present tense

 # if adjective/determiner is followed by potential adjective/noun and then potential noun/verb and then prep or comma or conjuection or ( then must resolve whether item is verb or not)
 -2 IS DETERMINER ADJECTIVE_BITS  - ADJECTIVE_PARTICIPLE
	HAS NOUN_PROPER_SINGULAR NOUN_SINGULAR	# an Afrikaner *farm worth anything
	INCLUDE * VERB_PRESENT 
	IS PREPOSITION COMMA PAREN CONJUNCTION
DISCARD

# present tense cannot exist with potential be (will be participle or adjective)
REVERSE 0	INCLUDE * VERB_PRESENT VERB_PRESENT_3PS
	SKIP IS  DETERMINER_BITS ADJECTIVE_BITS ADVERB_BITS NOUN_BITS PRONOUN_BITS 
	ISCANONICAL be
DISCARD

# potential verb after "be" cannot be present (would have to be participle)
-1	ISCANONICAL be
	INCLUDE * VERB_PRESENT VERB_PRESENT_3PS
DISCARD

### cant be past tense

# no simple past after aux  "her fashion sense was usually *described as bizarre" - must be past participle
REVERSE 0 INCLUDE * VERB_PAST
	SKIP IS ADVERB_BITS
	IS AUX_VERB_BITS
DISCARD

# cant be singular verb with plural noun in front  "the stars signed *autographs" - but if noun were part of prep phrase we would be wrong... "the star by the home signed autographs" 
REVERSE 0	INCLUDE * VERB_PRESENT_3PS
	SKIP !HAS NOUN_BITS PRONOUN_BITS	# nearest potential subject of verb
	IS NOUN_PLURAL		# is known as a noun 
	SKIP !HAS NOUN_BITS PRONOUN_BITS	# is there another in front of that?
	!HAS NOUN_BITS PRONOUN_BITS # no, this is the only subject around
DISCARD

### cant be present singular 3rd person
# potential singular verb after plural pronoun	- 
# potential singular verb after plural noun	-  
# potential plural verb after singular noun	- not "scientists who have studied the coyote *believe " 
# potential plural verb after singular pronoun	- but might be clause object

### cant be participle

# participle after noun/pronoun will not be verb (will be adjective_participle) unless have before that...  "I am the boy *singing in the woods" but not in question format "is the man going"  but not "has the man gone"
-1	IS  NOUN_BITS PRONOUN_BITS #  "he *walked past it" - it will be past tense (no helper) - except "has the man gone" using a helper verb in question form
	INCLUDE * VERB_PRESENT_PARTICIPLE VERB_PAST_PARTICIPLE
	!ISQUESTION aux	qword	# aux could allow intrusion of subject between aux and verb - but beware "is the man swimming in the river going home? 
DISCARD

# past participle must occur sometime after have or be or get for perfect tenses or after be for passive voice 
REVERSE 0 INCLUDE * VERB_PAST_PARTICIPLE 
	SKIP !HAS AUX_VERB_BITS VERB_TENSES
	!HAS AUX_BE AUX_HAVE
DISCARD

# verb participle is special adjective kind et
DISCARD

# verb participle requires have verb or appropriate aux (be have get) somewhere before it - "problems often become *frustrating"
REVERSE 0 INCLUDE * VERB_PAST_PARTICIPLE VERB_PRESENT_PARTICIPLE
	SKIP !HAS AUX_VERB_BITS 
	!HAS AUX_BE AUX_HAVE
DISCARD

# participle cant follow potential conjunction or preposition (will be noun_gerund or adjective) # after running home I walked.  although walking dead don't run, I do.
-1 HAS PREPOSITION CONJUNCTION_SUBORDINATE CONJUNCTION_COORDINATE		
	INCLUDE * VERB_PRESENT_PARTICIPLE VERB_PAST_PARTICIPLE
DISCARD

### can only be participle

# passive voice using be with past participle   "all men are created equal"  and present continuous uses  "I am swimming"
REVERSE 0 INCLUDE * VERB_TENSES - VERB_PAST_PARTICIPLE  - VERB_PRESENT_PARTICIPLE 
	SKIP IS ADVERB_BITS
	HAS AUX_BE	
DISCARD

### can only be past participle

# can ONLY have past participle with have statement  "I have been {adverb} *gone" but "I have studied before I moved to the US" - "have you had"
REVERSE 0	INCLUDE * VERB_TENSES - VERB_PAST_PARTICIPLE  
	STAY !HAS AUX_VERB_BITS 
	SKIP !HAS AUX_VERB_BITS VERB_PAST_PARTICIPLE	# skip back til you find the aux before this - this handles "I have gone" and "I have been gone" or you find an earlier past participle
	ISCANONICAL have
DISCARD

# passive voice using get with past participle   "all men get created equal"
REVERSE 0 INCLUDE * VERB_TENSES - VERB_PAST_PARTICIPLE 
	SKIP IS ADVERB_BITS
	ISCANONICAL get
DISCARD

### can only be present participle

# present participle must occur sometime after "been" or present/past/infinitive tense "be" to remain a tense verb
REVERSE 0 INCLUDE * VERB_PRESENT_PARTICIPLE 
	SKIP !HAS AUX_BE
	!HAS AUX_BE		
DISCARD

# can ONLY have present participle with have/had been (progressive) statement "I have/had {adverb} been *going" or for passive  but "I am *being taken" means could also be aux 
REVERSE 0	INCLUDE * VERB_TENSES - VERB_PRESENT_PARTICIPLE  - VERB_PAST_PARTICIPLE  
	SKIP IS ADVERB_BITS
	IS AUX_BE		# been   - "the castle has been visited for the past 5 years"
	STAY ORIGINALVALUE VERB_PAST_PARTICIPLE
	SKIP !HAS AUX_VERB_BITS 		# skip back til you find the aux before this - to infinitive blocks
	ISCANONICAL have 
DISCARD

### can only be infinitve

# cannot have ordinary verb tense immediately after TO "I saw George who continued to *play videogames" except infinitive
-1	ISCANONICAL to
	INCLUDE * VERB_TENSES - VERB_INFINITIVE
DISCARD

# future tense can only have infininitve  "I will go" unless following have as in  "I will have gone" and "I will have been gone" but not "I will say what I *like"
REVERSE 0	INCLUDE * VERB_TENSES - VERB_INFINITIVE   
	STAY !HAS AUX_VERB_BITS 
	SKIP !HAS AUX_VERB_BITS TO_INFINITIVE PRONOUN_BITS NOUN_BITS	# skip back til you find the aux before this - but have been and TO infinitive blocks
	IS AUX_VERB_FUTURE
DISCARD

# only tense of a verb that can start a sentence is infinitive  "mother let her daughter go" "pencils for the teacher" , "search for my nails" but not "Go home" 
0	INCLUDE * VERB_TENSES - VERB_INFINITIVE
	STAY START
DISCARD

### can not be infinitive

# if we can be infinitive and present and no other verbs or aux or to can be before us and we are not imperative first place, be present   "problems often *become frustrating" but not  "why not ask for help"
# NOT "let us go for a walk"

# cannot have verb infinitive as 2nd word "what *shape is this" except "To *go would seem foolish" except "please have"
-1	START
	STAY !ISORIGINAL TO
	STAY !HAS ADVERB_BITS
	INCLUDE * VERB_INFINITIVE
DISCARD

# given we KNOW the following word is a verb (infinitive or present) current word CANNOT be infinitive  "please *bring coffee"
0 INCLUDE * VERB_INFINITIVE
	STAY HAS ADVERB_BITS 
	HAS VERB_INFINITIVE VERB_PRESENT
	STAY !HAS ADVERB_BITS NOUN_BITS ADJECTIVE_BITS
DISCARD

# verb infinitive cannot follow a gerund or adj. participle or to infinitive - "I remembered to eat food after *walking home"
-1	IS NOUN_GERUND ADJECTIVE_PARTICIPLE VERB_INFINITIVE
	INCLUDE * VERB_INFINITIVE
DISCARD

 # infinitives do not follow or other infinitives -- can follow pronoun subject (in a question) or object 
REVERSE 0	INCLUDE *  VERB_INFINITIVE 
	SKIP IS ADVERB_BITS	#not: I *guess
	IS  VERB_INFINITIVE NOUN_INFINITIVE  # I could not (wish) for more. 
DISCARD 

# cannot have verb infinitive BEFORE an aux 
0	INCLUDE *  VERB_INFINITIVE 
	IS AUX_VERB_BITS		# no *man has gone before
DISCARD

# in a present/past conflict, prefer the past (GUESS) "he read the book" or "he let the room"
0	INCLUDE *  VERB_PRESENT
	STAY HAS VERB_PAST
DISCARD

 # linking verb vs aux verb conflict resolved if followed by common participle verb  "he is interesting"
 0 INCLUDE * VERB_TENSES
	STAY HAS AUX_VERB_BITS
	HASCANONICALPROPERTY COMMON_PARTICIPLE_VERB
KEEP

#   "you must not *smoke here"  "problems often *become frustrating"

############################################
###  RULES ABOUT VERB INFINITIVES
############################################

# ---- infinitives without TO are these

# cannot have verb infinitive after a q word "I wonder what *book" EXCEPT "why stand up"
-1  ISQWORD
	STAY !ISORIGINAL why
	INCLUDE * VERB_INFINITIVE
DISCARD

# following a sooner ... than clause- "he would sooner wash than wipe"
REVERSE 0	INCLUDE * VERB_INFINITIVE
	ISORIGINAL than
	SKIP !ISORIGINAL sooner
	ISORIGINAL sooner
KEEP

# following used_to  "he used to swim"
REVERSE 0 INCLUDE * VERB_INFINITIVE
	SKIP IS ADVERB_BITS
	ISCANONICAL used_to
KEEP

# following a rather ... than clause- "he would rather wash than wipe"
REVERSE 0	INCLUDE * VERB_INFINITIVE
	ISORIGINAL than
	SKIP !ISORIGINAL rather
	ISORIGINAL rather
KEEP

# infinitive after verb of perception + object - "she feels the rain *fall" or  "I heard Peter *sing"  # also let and help and make "she let the rain *fall"   "She made Peggy and Samantha clean "  but not "I would sooner read a book than watch this *film"
  # also "Let us go home"

# infinitive after modal - "he can *run"  and "I must not *go" but dont lose adverb "can *still run"
REVERSE 0 INCLUDE * VERB_INFINITIVE
	STAY !ISCANONICAL be		# "he will be running" is not infinitive
	STAY !HAS ADVERB_BITS
	SKIP ISORIGINAL not
	IS AUX_VERB_TENSES AUX_DO
KEEP

# infinitive after modal in command mode "let us go"
REVERSE 0 INCLUDE * VERB_INFINITIVE
	STAY !ISCANONICAL be
	HAS NOUN_BITS PRONOUN_BITS  # the object before the infinitive
	SKIP IS DETERMINER ADJECTIVE_BITS ADVERB_BITS
	IS VERB_INFINITIVE 
	STAY START
	STAY HASCANONICALPROPERTY VERB_TAKES_VERBINFINITIVE
KEEP

# infinitive after original modal as verb - "you need not *go" but not "he can run very *fast" but not "what will my dog do with the next *ball" - but not "he can *still smell"
REVERSE 0 INCLUDE * VERB_INFINITIVE
	STAY !HAS AUX_BE
	STAY !HAS ADVERB_BITS
	SKIP !ORIGINALVALUE AUX_VERB_BITS VERB_INFINITIVE TO_INFINITIVE NOUN_BITS ADJECTIVE_BITS DETERMINER_BITS # had the old plastic *dog"
	IS AUX_VERB_BITS 
	STAY !ISCANONICAL be
	STAY !ISCANONICAL have
KEEP

# infinitive after do "I do not *know" EXCEPT "I do *better" where it might be adverb
REVERSE 0 INCLUDE * VERB_INFINITIVE
	STAY !HAS ADVERB_NORMAL
	SKIP ISORIGINAL not
	ISCANONICAL do
KEEP

# had better expression makes infinitive - "You had better *clean up your room."
-2  ISORIGINAL had
	ISORIGINAL better
	INCLUDE * VERB_INFINITIVE
KEEP

# would rather expression makes infinitive - "You would rather *clean up your room."
-2 	ISORIGINAL would
	ISORIGINAL rather
	INCLUDE * VERB_INFINITIVE
KEEP

# would sooner expression makes infinitive - "You would sooner *clean up your room than watch this film."
-2	ISORIGINAL would
	ISORIGINAL sooner
	INCLUDE * VERB_INFINITIVE
KEEP
 
# why not expression makes infinitive - "Why not *ask your neighbour for help"
-2  ISORIGINAL why
	ISORIGINAL not
	INCLUDE * VERB_INFINITIVE
KEEP

#  "Why should we *go by car" and "Why should we not *go by car? "

# adverb at start followed by infinitive will be a command (not a noun)  "boldly go home"  - but not after why as in "why don't I go"
-1	IS ADVERB_BITS
	STAY START
	STAY !ISORIGINAL why
	STAY !ISMEMBER ~negative_adverb_starter 
	INCLUDE * VERB_INFINITIVE
KEEP

# infinitive/present conflict after do, will be infinitive "do wildflowers grow"  but not tag questions, ", *do you"

# To before a verb infinitive will be noun infinitive - "I like to *play"
-1 ISCANONICAL to
	INCLUDE * VERB_INFINITIVE
DISCARD

# anything not known to be to-less infinitive is not infinitive  - but not "what will my dog *do with the next ball" but causal to-less verbs also accept

# all other infinitives require TO_INFINITIVE

# infinitive exists in 4 cases: imperative sentence, to_infinitive constructions, subjunctive mood, and simple future "I will *go home"

#1.Contrary-to-fact clauses beginning with if: "If I were two-faced, would I be wearing this one?"
#2.Contrary-to-fact clauses expressing a wish: "At that moment, I had the most desperate wish that she were dead."
#3.That clauses after verbs such as ask, demand, insist, propose, request, and suggest, recommend: "I demand that he leave at once."
#4.Statements of necessity: "It's necessary that she be in the room with you."
#5.Certain fixed expressions: as it were, be that as it may be, far be it from me, heaven forbid, if need be, so be it, suffice it to say

# Imperatives lack subject and use the base form of the verb. They may have clause, phrases and/or adverbs before them.
# If this is coffee, please bring me some tea; but if this is tea, please bring me some coffee.
# when you come home, go quickly.

# it will be INFINITIVE with optional adverb after future aux "I will boldly *go" EXCEPT for "be" which might be more aux as in "I will *be going" and "have"  as in "I will have been going" but not "jack can still smell"
REVERSE 0	INCLUDE * VERB_INFINITIVE
	STAY !ISCANONICAL be
	STAY !ISCANONICAL have
	STAY !HAS ADVERB_BITS
	SKIP IS ADVERB_BITS
	IS AUX_VERB_FUTURE
KEEP

# it will be present participle with optional adverb after be and future aux "I will be boldly *going" - but not "it will have been *being painted"
REVERSE 0	INCLUDE * VERB_PRESENT_PARTICIPLE
	STAY !ISCANONICAL be
	SKIP IS ADVERB_BITS
	ISCANONICAL be
	SKIP !HAS AUX_VERB_FUTURE		# search as far as you have to go, even in question form
	IS AUX_VERB_FUTURE
KEEP

# will be imperative infinitive at start when  followed by some  things like pronoun: "Jump him"  but not adverbs, like "*right now, he left" and not "*mother let her daughter free"
# BUT NOT "*like what does she look"

# imperative negative infinitive at start "do not ever *go there"
REVERSE	0	INCLUDE * VERB_INFINITIVE 
	SKIP IS ADVERB_BITS
	ISORIGINAL not		
	ISORIGINAL do
	STAY START
KEEP

# passive: I was hit by Bob  but not "I was fascinated by lightning"
-1	HAS AUX_VERB_BITS 
	INCLUDE * VERB_TENSES
	STAY !HAS ADJECTIVE_PARTICIPLE
	ISORIGINAL by
KEEP



############################################
########### Rules about verb plurality
############################################


# verb plurality agreements are tricky because noun in front might be in a phrase or clause
# uses guess2 so assignroles has a chance to fill in objects of verbals and clauses

# wont have a verb immediately after a gerund?  FALSE:  Swimming *was his passion. But "After walking *home" home is not a verb


############################################
########### Rules about verb tenses
############################################
# past tense cannot start a sentence -- BUT we prefer it over past_participle for reduced clause detection -- Driven is a participle that will show up
# dressed in red will start as verb_past, be modified later
# dressing in red  is not a verb, its a noun gerund

# beware I will, when we are ready, be going home.

#### PASSIVE

# PASSIVE SIMPLE PRESENT/PAST [Thing receiving action] + [be] + [past participle of verb] + [by] + [thing doing action] "the tree is/was cleaned by Tom"
REVERSE -1	ISCANONICAL by
	INCLUDE * VERB_PAST_PARTICIPLE
	SKIP IS ADVERB_BITS
	ISCANONICAL be
KEEP

# PASSIVE PRESENT/PAST CONTINUOUS [Thing receiving action] + is/was being + [past participle of verb] + [by] + [thing doing action] "the tree is being cleaned by Tom"
REVERSE -1	ISCANONICAL by
	INCLUDE * VERB_PAST_PARTICIPLE
	SKIP IS ADVERB_BITS
	ISORIGINAL being
	ISCANONICAL be
KEEP

# PASSIVE PRESENT/PAST PERFECT [CONTINUOUS Thing receiving action] + has/had been + [past participle of verb] + [by] + [thing doing action] "the tree has been cleaned by Tom"
REVERSE -1	ISCANONICAL by
	INCLUDE * VERB_PAST_PARTICIPLE
	SKIP IS ADVERB_BITS
	ISORIGINAL being
	ISORIGINAL been
	SKIP IS ADVERB_BITS
	ISCANONICAL have
KEEP

# PASSIVE SIMPLE FUTURE  The work will be finished by 5:00 PM.
REVERSE 0 INCLUDE * VERB_PAST_PARTICIPLE
	SKIP IS ADVERB_BITS
	ISORIGINAL be
	SKIP IS ADVERB_BITS
	ISCANONICAL will
KEEP

# PASSIVE SIMPLE FUTURE  The work is going to be finished by 5:00 PM.
REVERSE 0 INCLUDE * VERB_PAST_PARTICIPLE
	SKIP IS ADVERB_BITS
	ISORIGINAL be
	SKIP IS ADVERB_BITS
	ISCANONICAL to
	ISCANONICAL going
	ISCANONICAL be
	STAY IS VERB_PRESENT VERB_PRESENT_3PS
KEEP

# PASSIVE  FUTURE CONTINUOUS The work will/isgoingto be being finished by 5:00 PM.
REVERSE 0 INCLUDE * VERB_PAST_PARTICIPLE
	SKIP IS ADVERB_BITS
	ISORIGINAL being
	SKIP IS ADVERB_BITS
	ISORIGINAL be
KEEP

# PASSIVE  FUTURE PERFECT The work will have been finished by 5:00 PM.
REVERSE 0 INCLUDE * VERB_PAST_PARTICIPLE
	SKIP IS ADVERB_BITS
	ISORIGINAL been
	SKIP IS ADVERB_BITS
	ISORIGINAL have
KEEP

# PASSIVE FUTURE PERFECT CONTINUOUS The work will have been being finished by 5:00 PM.
REVERSE 0 INCLUDE * VERB_PAST_PARTICIPLE
	SKIP IS ADVERB_BITS
	ISORIGINAL being
	ISORIGINAL been
	SKIP IS ADVERB_BITS
	ISORIGINAL have
KEEP

# keep forms

# past participle after BE verb  "it was *outmoded"  or "I would like to have been *fired" will be passive voice tense but not "he was *interested"
REVERSE 0  INCLUDE *  VERB_PAST_PARTICIPLE
	STAY HAS   ADJECTIVE_PARTICIPLE
	STAY !HASPROPERTY COMMON_PARTICIPLE_VERB
	SKIP IS ADVERB_BITS
	ISCANONICAL be
KEEP

# non-be verb following std aux (not be or have) will be infinitive  " I do often *go" - we dont have AUX_VERB_INFINTIIVE as a tense... using AUX_VERB_PRESENT - but not adverb- "he can *still smell"
REVERSE 0	INCLUDE * VERB_INFINITIVE
	STAY !ISCANONICAL be
	STAY !HAS ADVERB_BITS
	SKIP IS ADVERB_BITS
	IS AUX_VERB_BITS
	STAY !ISCANONICAL have
	STAY !ISCANONICAL be
KEEP

#   present/future perfect tense uses past participle - I have traveled. I will have travelled
-1	IS AUX_VERB_PRESENT
	STAY ISCANONICAL have
	INCLUDE * VERB_PAST_PARTICIPLE # have been
	STAY !HAS AUX_VERB_BITS
KEEP

# "used to" aux forces verb infinitive "I used_to go"
-1 IS AUX_VERB_BITS
	STAY ISCANONICAL used_to
	INCLUDE * VERB_INFINITIVE
KEEP

  
# something cannot be a verb participle without having a potential aux verb before it - "has John who *lied yesterday *lied today?"
# EXCEPT for an absolute phrase like "they walked, their faces *beaming"

# There are simple, progressive, perfect, and perfect progressive tense zones

### SIMPLE TENSES - past present future

#  simple future is infinitive when not BE and not adverb "he could *still smell"
-1	IS AUX_VERB_FUTURE		# I will go.  I would go.  I could go.
	INCLUDE * VERB_INFINITIVE
	STAY !ISCANONICAL be
	STAY !HAS ADVERB_BITS
KEEP

#  simple future delayed is infinitive when not be and not adverb "he could *still smell"
-2	IS AUX_VERB_FUTURE
	!HAS VERB_TENSES AUX_VERB_BITS 
	INCLUDE * VERB_INFINITIVE		# I will often go
	STAY !ISCANONICAL be
	STAY !HAS ADVERB_BITS
KEEP

# simple present after do "do you write" or "you do write" but not "did somebody make you *wear that hat" and not "I do better" as adjective
REVERSE 0	INCLUDE * VERB_PRESENT
	STAY !HAS ADVERB_NORMAL
	SKIP !IS AUX_VERB_BITS VERB_TENSES
	IS AUX_DO 
KEEP

# passive present	-  the product is shipped
-1 IS AUX_VERB_PRESENT
	STAY ISCANONICAL be	
	INCLUDE * VERB_PAST_PARTICIPLE
KEEP

# passive modal - the product can be shipped
-2 IS AUX_VERB_PRESENT
	ISORIGINAL be
	INCLUDE * VERB_PAST_PARTICIPLE
KEEP

# passive present delayed	-  the product is often shipped
-2 IS AUX_VERB_PRESENT
	STAY ISCANONICAL be	
	!HAS VERB_TENSES AUX_VERB_BITS 
	INCLUDE * VERB_PAST_PARTICIPLE
KEEP


# passive past	-  the product was shipped
-1 IS AUX_VERB_PAST
	STAY ISCANONICAL be	
	INCLUDE * VERB_PAST_PARTICIPLE
KEEP

# passive past delayed	-  the product was often shipped
-2 IS AUX_VERB_PAST
	STAY ISCANONICAL be	
	!HAS VERB_TENSES AUX_VERB_BITS 
	INCLUDE * VERB_PAST_PARTICIPLE
KEEP

### PROGRESSIVE TENSES   use present participle

## Past Progressive uses forms of "was" and present participle

#   past progressive tense is participle - but not he was being helped
-1	IS AUX_VERB_PAST
	STAY ISCANONICAL be  # I was going
	INCLUDE * VERB_PRESENT_PARTICIPLE
	STAY !ISCANONICAL be
KEEP

#   past progressive tense indirect is participle 
-2	IS AUX_VERB_PAST
	STAY ISCANONICAL be 
	!HAS VERB_TENSES AUX_VERB_BITS    # I was not going.  I was often going.
	INCLUDE * VERB_PRESENT_PARTICIPLE
KEEP

## Present Progressive uses forms of "be" and present participle

#   present progressive tense is participle 
-1	IS AUX_VERB_PRESENT
	STAY ISCANONICAL be  # I am going
	INCLUDE * VERB_PRESENT_PARTICIPLE
	STAY !ISCANONICAL be
KEEP

#   present progressive tense indirect is participle -  I am not going.  I am often going.
-2	IS AUX_VERB_PRESENT
	STAY ISCANONICAL be 
	!HAS VERB_TENSES AUX_VERB_BITS   
	INCLUDE * VERB_PRESENT_PARTICIPLE
	STAY !ISCANONICAL be
KEEP

# present/past progressive passive - the food is/was being prepared
-2	IS AUX_VERB_PRESENT AUX_VERB_PAST 
	STAY ISCANONICAL be
	ISORIGINAL being
	INCLUDE * VERB_PAST_PARTICIPLE
	STAY !ISCANONICAL be
KEEP

# present/past progressive passive - the food is/was often being prepared
-3	IS AUX_VERB_PRESENT AUX_VERB_PAST 
	STAY ISCANONICAL be
	!HAS VERB_TENSES AUX_VERB_BITS   
	ISORIGINAL being
	INCLUDE * VERB_PAST_PARTICIPLE
KEEP

# future progressive passive - the food will  be prepared
-2	IS AUX_VERB_FUTURE
	ISORIGINAL be
	INCLUDE * VERB_PAST_PARTICIPLE
KEEP

# future progressive passive delayed - the food will often be prepared
-3	IS AUX_VERB_FUTURE
		 ISORIGINAL be
	!HAS VERB_TENSES AUX_VERB_BITS   
	INCLUDE * VERB_PAST_PARTICIPLE
KEEP

## Future Progressive uses forms of "will be" "shall be" etc and present participle

#   future progressive tense is participle - I will be going
-2	IS AUX_VERB_FUTURE
	ISORIGINAL be 
	INCLUDE * VERB_PRESENT_PARTICIPLE
	STAY !ISORIGINAL be		# being could have more after it
	STAY !ISORIGINAL being
KEEP

#   future progressive tense indirect is participle --  I will not be going.  I will often be going.
-3	IS AUX_VERB_FUTURE
	!HAS VERB_TENSES AUX_VERB_BITS   
	ISORIGINAL be 
	INCLUDE * VERB_PRESENT_PARTICIPLE
	STAY !ISORIGINAL be		# being could have more after it
KEEP

################### PERFECT TENSE uses has/have with the past participle " I have broken my arm"
 
# passive voice uses past participle with past Be verb. "the window was broken"
# adjective participle? is used after present tense be verb.  "the window is broken"




## Present Perfect uses forms of "has" 


#   present/future perfect tense delayed uses past participle - I have not traveled. 
-2	IS AUX_VERB_PRESENT
	STAY ISCANONICAL have 
	!HAS VERB_TENSES AUX_VERB_BITS   
	INCLUDE * VERB_PRESENT_PARTICIPLE
KEEP


#   present/past/future perfect passive tense delayed uses past participle - they have often been made
BIGWORD -3	IS AUX_VERB_PRESENT IS_AUX_VERB_PAST
	STAY ISCANONICAL have 
	!HAS VERB_TENSES AUX_VERB_BITS   
	ISORIGINAL been
	INCLUDE * VERB_PRESENT_PARTICIPLE
KEEP

## Past Perfect uses forms of "had" 

#   past perfect tense uses past participle - I had gone x2
-1	ISORIGINAL had
	INCLUDE * VERB_PAST_PARTICIPLE
	STAY !HAS AUX_BE
KEEP

#   past perfect tense delayed uses past participle -  I had not gone.  I had often gone. x1
-2	ISORIGINAL had 
	!HAS VERB_TENSES  AUX_VERB_BITS   
	INCLUDE * VERB_PAST_PARTICIPLE
	STAY !HAS AUX_BE
KEEP

###################### PERFECT PROGRESSIVE

## Present Perfect progressive uses forms of has/have been and present participle

#   present/future perfect progressive tense uses present participle
-2	ISCANONICAL have
	STAY IS VERB_PRESENT
	ISORIGINAL been
	INCLUDE * VERB_PRESENT_PARTICIPLE # I have been going
KEEP

#   present/future perfect progressive tense delayed uses present participle
-3	ISCANONICAL have
	STAY IS VERB_PRESENT
	!HAS VERB_TENSES  AUX_VERB_BITS   # I have not been going.
	ISORIGINAL been 
	INCLUDE * VERB_PRESENT_PARTICIPLE 
KEEP

## Past Perfect progressive uses had been and present participle but not "been being"

#   past perfect progressive tense uses present participle
-2	ISORIGINAL had
	ISORIGINAL been
	INCLUDE * VERB_PRESENT_PARTICIPLE # I had been going
	STAY !HAS AUX_BE
KEEP

#   past perfect progressive delayed  tense uses present participle
-3	ISORIGINAL had
	!HAS VERB_TENSES  AUX_VERB_BITS   # I have not been going.
	ISORIGINAL been 
	INCLUDE * VERB_PRESENT_PARTICIPLE 
KEEP

#######################



# past participle has have aux  in front  - "the man has slept" 
-1	IS AUX_VERB_BITS
	STAY ISCANONICAL have
	INCLUDE * VERB_PAST_PARTICIPLE
KEEP

# present participle has be aux  in front  - "the man is *sleeping" 
-1	IS AUX_VERB_BITS
	STAY ISCANONICAL be
	STAY !HAS AUX_BE	# not been
	STAY !ORIGINALVALUE VERB_PAST_PARTICIPLE
	INCLUDE * VERB_PRESENT_PARTICIPLE
	STAY !HAS AUX_VERB_BITS
KEEP
# which almost *ruined

 # are you most closely *attached to this
 # who was the meat eaten by

# will be simple past after conjunction_suborde, subject, verb
-2 IS CONJUNCTION_SUBORDINATE
	IS PRONOUN_SUBJECT NOUN_BITS
	INCLUDE * VERB_PAST	# that jack built
KEEP


############################################
########### Rules about verbs
############################################

# BUt-- I hit him after his shift waiting tables (treat as ADJECTIVE_PARTICPILE phrase)
# ILLEGAL the *swimming boy flies. The happy *swimming boy flies.
# ILLEGAL as verb: the boy *swimming nearby
# BUT- I liked the swimming is ok.
# wont have a verb BEFORE a subject pronoun (except if is/have in question mode or IMPLIED clause like I guess we will go)
# cannot have two verbs in a row followed by a pronon or noun or determiner - yes you can-- boys boys like like boys
# we think you cant have a verb before a verb (except maybe for some clauses) so yes you can
# cannot have verb at end of sentence after another verb - other verb completing a phrase would be weird -- sure you can-- The men who dressed won



# if preceeding IS a verb and next word MIGHT be, its not-  "my friend Chris eats *rocks"  (though maybe participle will prove an exception) but "what they found *thrilled them" so have clause isssues

# like before to   will be a verb - "what game does Bob like to play  -- treat as noun infinitive

# aux question with pronoun option will want verb after it -- did you go to parties
-2	HAS AUX_VERB_BITS
	HAS PRONOUN_BITS
	INCLUDE * VERB_TENSES
KEEP

# assume verb ? *? verb  will involve a described noun
-2	IS VERB_TENSES
	HAS ADJECTIVE_BITS NOUN_BITS
	INCLUDE * NOUN_BITS
	IS VERB_TENSES AUX_VERB_BITS		# the team that wore red shirts fled
KEEP


# simple present - Tom cleans the house

# verb before conjunction coordinate, verb after?
-2 IS VERB_TENSES
	IS CONJUNCTION_COORDINATE	# I like walking and swimming
	INCLUDE * VERB_TENSES
KEEP

# potential singular verb after singular noun = bad rule
# LEGAL: - this report *documents  ILLEGAL: He has been stalking neighborhood *pets

# potential singular verb after singular noun	- this  *documents
-1	IS PRONOUN_BITS
	INCLUDE * VERB_PRESENT_3PS
	STAY !HAS AUX_VERB_BITS
KEEP

# potential plural present verb after plural noun	- these reports *document - BUT NOT if aux "do you *grow"

#  simple future/continuous - tom will be washing the dishes and present continuous - Tom is going home and passive simple present/past - the house is/was cleaned and present perfect - Tom has been doing the work.
REVERSE 0 INCLUDE * VERB_PRESENT_PARTICIPLE VERB_PAST_PARTICIPLE
	STAY !HAS AUX_VERB_BITS
	SKIP IS ADVERB_BITS			# we are often suffering
	ISCANONICAL be				
	STAY !ISORIGINAL being				# but not After being reprimanded
	STAY IS AUX_VERB_BITS
KEEP

# passive present/past continuous - Tom is/was being helped and passive present perfect - the work has been being done by john
REVERSE 0 INCLUDE * VERB_PAST_PARTICIPLE
	ISORIGINAL being
	SKIP IS ADVERB_BITS			# we are often suffering
	ISCANONICAL be
	STAY IS AUX_VERB_BITS
KEEP

# past perfect continuous - TOM had been repairing his car. and passive future perfect and future perfect continuous and passive past perfect - "Cars had been *repaired by Tom"  but not "the work has *been being done"
 REVERSE 0	INCLUDE * VERB_PRESENT_PARTICIPLE VERB_PAST_PARTICIPLE  # but not followed by past_participle "the work had *been being done"
	STAY !HAS AUX_BE
	ISORIGINAL been
	SKIP IS ADVERB_BITS			# we are often suffering
	ISCANONICAL have	
KEEP

# passive simple/continuous future - the dishes will be being washed by tom and  passive past perfect continuous and  passive future perfect continuous-  the car had been being repaired by tom
REVERSE 0	INCLUDE * VERB_PAST_PARTICIPLE 
	ISORIGINAL being
	SKIP IS ADVERB_BITS			# we are often suffering
	ISCANONICAL be
KEEP

# future perfect and  past perfect	Tom had repaired the car
REVERSE 0	INCLUDE * VERB_PAST_PARTICIPLE
	STAY !HAS AUX_VERB_BITS
	SKIP IS ADVERB_BITS			# we are often suffering
	ISCANONICAL have
KEEP

# past vs past participle, use past when no aux before -- no good when involving absolutes "arms *folded, they lept"
# but good here the mangled boats *floated

# after conjunction_subordinate, try verb phrase instead of adjective, noun (no determiner)
# He hated and infuriated Malcolm. -- Not He hated cars and infuriated people. (adjective)4

# They can often *outmode. -- but not "they were *outmoded" which should be adjective participle

#  noun following potential verb be		
0	ISCANONICAL be # 	# There is nothing here.
	STAY INCLUDE  * VERB_TENSES
	IS NOUN_BITS 
KEEP  

# "be" followed by potential noun-adjective and not verb or adverb is normal - and not "been being"
0	ISCANONICAL be
	STAY INCLUDE * VERB_TENSES	# ILLEGAL:  I am going. I am constantly outmoded. LEGAL: I am humanity. I am happy.
	HAS NOUN_BITS ADJECTIVE_BITS	
	STAY !HAS VERB_PRESENT_PARTICIPLE VERB_PAST_PARTICIPLE VERB_PAST VERB_PRESENT VERB_PRESENT_3PS ADVERB_BITS
	STAY !ISORIGINAL being
KEEP

# be followed by adjective with intervening adverbs 
0	ISCANONICAL be
	STAY INCLUDE  * VERB_TENSES	
	SKIP IS ADVERB_BITS
	IS ADJECTIVE_BITS  
KEEP

# present participle after aux verb
-1	IS AUX_VERB_BITS
	STAY ISCANONICAL be
	INCLUDE  * VERB_PRESENT_PARTICIPLE
	STAY !HAS AUX_VERB_BITS
KEEP

# verb "be" followed by adjective participle potential will realize that potential "he *is boring"
0	INCLUDE * VERB_TENSES
	STAY ISCANONICAL be
	SKIP HAS ADVERB_BITS		# can this survive w/o being IS form?
	HAS ADJECTIVE_PARTICIPLE
	STAY HASPROPERTY COMMON_PARTICIPLE_VERB
KEEP
